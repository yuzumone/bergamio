apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getSecretProperty(name) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('secret.properties').newDataInputStream())

    return properties.getProperty(name.toString())
}

def SCHEME = getSecretProperty('SCHEME')
def HOST = getSecretProperty('HOST')
def DEVELOPER_ID = "\"${getSecretProperty('DEVELOPER_ID')}\""
def REDIRECT_URI = "\"${getSecretProperty('REDIRECT_URI')}\""

android {
    compileSdkVersion 27
    dataBinding {
        enabled = true
    }
    androidExtensions {
        experimental = true
    }
    defaultConfig {
        applicationId "net.yuzumone.bergamio"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 4
        versionName "1.1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [scheme: SCHEME, host: HOST]
        buildConfigField 'String', 'DEVELOPER_ID', DEVELOPER_ID
        buildConfigField 'String', 'REDIRECT_URI', REDIRECT_URI
    }
    buildTypes {
        release {
            debuggable false
            zipAlignEnabled true
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

ext.support_lib_version = '27.0.2'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "com.android.support:appcompat-v7:$support_lib_version"
    implementation "com.android.support:design:$support_lib_version"
    implementation "com.android.support:cardview-v7:$support_lib_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    kapt 'com.android.databinding:compiler:3.0.1'
    implementation 'com.google.dagger:dagger:2.12'
    kapt 'com.google.dagger:dagger-compiler:2.12'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
    implementation 'io.reactivex:rxkotlin:0.60.0'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxjava:1.3.0'
}
